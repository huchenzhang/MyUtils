//第一行代码应用了android程序的gradle插件，作为android的应用程序，第一步是必须的
//因为plugin中提供了android编译、测试、打包等的所有task
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
//这是编译文件中最大的代码块，关于android的所有特殊配置都在这里。
android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'
    //程序默认配置，如果AndroidMainfest.xml里面定义了与这里相同的属性，会以这里为主
    defaultConfig {
        //1.作为程序唯一标示，防止同一个手机重复安装同一个应用。
        //2.作为R文件的包名，改名不会报错
        applicationId "com.example.huchenzhang.myutils"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //兼容svg图
        vectorDrawables.useSupportLibrary = true
        //适配gradle:3.0以上版本
        flavorDimensions VERSION_CODE
    }
    //打包时用的签名和密码
    signingConfigs {
        signConfig {
            storeFile file(KEY_STORE_FILE_PATH)
            storePassword KEY_STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }
    //渠道包
    productFlavors {
        common {
            manifestPlaceholders = [channel: "common"]
        }
        xiaomi {
            manifestPlaceholders = [channel: "xiaomi"]
        }
        wandoujia {
            manifestPlaceholders = [channel: "wandoujia"]
        }
    }
    //定义了编译类型，针对每一个类型我们可以有不同的编译配置，不同的编译配置对应的有不同的编译命令。
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signConfig
        }
    }
    dataBinding {
        enabled = true
    }
    crashlytics {
        enableNdk true
        androidNdkOut 'src/main/obj'
        androidNdkLibsOut 'src/main/libs'
    }
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven {
            url "http://maven.aliyun.com/nexus/content/repositories/releases"
        }
    }

    //自定义apk安装包名
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "测试-${variant.buildType.name}-v${variant.versionName}-${variant.productFlavors[0].name}-${releaseTime()}.apk".toLowerCase()
        }
    }
}

//属于gradle的依赖配置，定义了当前项目所需要的依赖的其它库
static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

//百度语音合成
dependencies {
    compile files('libs/com.baidu.tts_2.3.1.20170808_e39ea89.jar')
    implementation files('libs/SocialSDK_QQ_Simplify.jar')
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.4'
    compile 'com.android.support:design:25.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.1.0-beta3'
    testCompile 'junit:junit:4.12'
    //数据库相关
    compile 'org.litepal.android:core:1.6.0'
    //上拉下拉刷新
    compile 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.3'
    //recyclerview
    compile 'com.android.support:recyclerview-v7:25.4.0'
    //gridlayout
    compile 'com.android.support:gridlayout-v7:25.4.0'
    //异步RxJava2
    compile 'io.reactivex.rxjava2:rxjava:2.0.5'
    //RxJava2 Android
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //RxJava2 Adapter
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    //广播
    compile 'com.parse.bolts:bolts-android:1.4.0'
    //网络
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.squareup.okio:okio:1.12.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    //retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    //Gson converter
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    //kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    //fabric，崩溃检测，用户量统计
    compile('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.6@aar') {
        transitive = true
    }
    //阿里云Sophix热修复
    compile 'com.aliyun.ams:alicloud-android-hotfix:3.1.6'
    //gson
    compile 'com.google.code.gson:gson:2.8.0'
    //picasso图片处理
    compile 'com.squareup.picasso:picasso:2.5.2'
}